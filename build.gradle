plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "com.github.breadmoirai.github-release" version "2.0.1"
}

group = 'atm.bloodworkxgaming'
version = '1.2.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.21'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.4'

    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    compile group: 'org.fusesource.jansi', name: 'jansi', version: '1.17.1'


    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
}

shadowJar {
    classifier = ""
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'atm.bloodworkxgaming.serverstarter.ServerStarter'
        )
    }
}


tasks.build.dependsOn shadowJar


task copyJar(type: Copy) {
    dependsOn build


    from file("$buildDir/libs/serverstarter-${version}.jar")
    into file("$buildDir/dist/serverstarter-$version/")
}

task packageDist(type: Copy) {
    group = "build"
    dependsOn copyJar

    from file("src/main/resources/startserver.bat")
    from file("src/main/resources/startserver.sh")
    from file("server-setup-config.yaml")


    into file("$buildDir/dist/serverstarter-$version/")

    filter { it.replaceAll("@@serverstarter-libVersion@@", version as String) }
}

task zipDist (type: Zip) {
    dependsOn packageDist
    group = "build"

    from file("$buildDir/dist/serverstarter-$version/")
    include '*'
    include '*/**'
    archiveName = "serverstarter-${version}.zip"
    destinationDir = file("$buildDir/release/")
}


githubRelease {
    token = getGithubKey()
    owner = "AllTheMods"
    repo = "ServerStarter"

    releaseAssets file("$buildDir/release/serverstarter-${version}.zip")
}

tasks.githubRelease.dependsOn zipDist

static String getGithubKey(){
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['GITHUB_TOKEN']
}