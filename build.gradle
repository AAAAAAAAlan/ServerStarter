plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

group = 'atm.bloodworkxgaming'
version = '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.21'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.4'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
}

shadowJar {
    classifier = ""
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'atm.bloodworkxgaming.serverstarter.ServerStarter'
        )
    }
}


tasks.build.dependsOn shadowJar


task copyJar(type: Copy) {
    dependsOn build


    from file("$buildDir/libs/serverstarter-${version}.jar")
    into file("$buildDir/dist/serverstarter-$version/")
}

task packageDist(type: Copy) {
    group = "build"
    dependsOn copyJar

    from file("src/main/resources/startserver.bat")
    from file("src/main/resources/startserver.sh")
    from file("server-setup-config.yaml")


    into file("$buildDir/dist/serverstarter-$version/")

    filter { it.replaceAll("@@serverstarter-libVersion@@", version as String) }
}

